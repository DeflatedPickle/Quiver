pluginManagement {
	plugins {
		id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
		id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlinVersion"

		id 'com.diffplug.spotless' version "$spotlessVersion"
		id 'de.undercouch.download' version "$downloadVersion"
		id "net.kyori.blossom" version "$blossomVersion"

		id 'com.github.ben-manes.versions' version "$gradleVersionsVersion"
		id 'se.ascp.gradle.gradle-versions-filter' version "$gradleVersionsFilterVersion"
		id 'se.patrikerdes.use-latest-versions' version "$gradleVersionsUseLatestVersion"
	}
}

rootProject.name = 'Quiver'

// Include the plugins
include 'master'

include 'core'
include 'launcher'

include 'filetable'
include 'foldertree'

include 'filepanel'
include 'textviewer'
include 'tableviewer'
include 'imageviewer'
include 'treeviewer'
include 'markdownviewer'
include 'spritesheetviewer'
include 'docxviewer'

include 'packexport'
include 'packsquashstep'
include 'zipstep'

include 'filewatcher'

include 'packcreator'
includeSub 'packcreator/rippedpack'
includeSub 'packcreator/emptypack'

// Include the libraries I use and own
// This is so I can fix any issues easier
[
	/*'discordrpc',*/
	'pluginmanager',
	'settingsgui',
	'tipoftheday',
	'swingsettings'
].each {
	include it
	project(":$it").projectDir = new File(settingsDir, "libs/sniffle/$it")
}

includeBuild 'libs/haruhi'
includeBuild 'libs/marvin'
includeBuild 'libs/monocons'
includeBuild 'libs/tosuto'
includeBuild 'libs/undulation'
includeBuild 'libs/mmf'

def includeSub(String path) {
	def name = path.split("/").last()
	include name
	project(":$name").projectDir = new File(settingsDir, path)
}
